plugins {
  id 'java'
  id 'org.springframework.boot' version '2.5.0'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

group = 'com.lojfacens'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'

repositories {
  mavenCentral()
  maven { url 'https://m2.dv8tion.net/releases' }
  maven { url 'https://jitpack.io' }
}

ext {
  jdaVersion            = '4.2.1_266'
  lavalinkVersion       = '3ea4125'
  lavaplayerVersion     = '8b65f55'
  youtubeRotatorVersion = '0.2.3'
  unirestVersion        = '1.4.9'
  reflectionsVersion    = '0.9.12'
}

dependencies {
  // discord
  implementation "net.dv8tion:JDA:${jdaVersion}"

  // music
  implementation ("com.github.freyacodes:Lavalink-Client:${lavalinkVersion}") {
    exclude group: "com.sedmelluq", module: "lavaplayer" }
  implementation "com.github.sedmelluq:lavaplayer:${lavaplayerVersion}"
  implementation "com.sedmelluq:lavaplayer-ext-youtube-rotator:${youtubeRotatorVersion}"

  // spring
  implementation "org.springframework.boot:spring-boot-starter"
  implementation "org.springframework.boot:spring-boot-configuration-processor"
  implementation "org.springframework.boot:spring-boot-starter-validation"

  // utils
  implementation "com.mashape.unirest:unirest-java:${unirestVersion}"
  implementation "org.reflections:reflections:${reflectionsVersion}"
  compileOnly "org.projectlombok:lombok"

  // annotation processors
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
  annotationProcessor "org.projectlombok:lombok"

  // test
  testImplementation "org.springframework.boot:spring-boot-starter-test"
}

test {
  useJUnitPlatform()
}

bootJar {
  // exclude profile with hardcoded sensitive data
  exclude('application-dev.yaml')
}
